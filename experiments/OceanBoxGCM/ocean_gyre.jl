#!/usr/bin/env julia --project
using ClimateMachine
ClimateMachine.init(parse_clargs = true)

using ClimateMachine.GenericCallbacks
using ClimateMachine.ODESolvers
using ClimateMachine.Mesh.Filters
using ClimateMachine.VariableTemplates
using ClimateMachine.Mesh.Grids: polynomialorder
using ClimateMachine.Ocean.HydrostaticBoussinesq

using Test

using CLIMAParameters
using CLIMAParameters.Planet: grav
struct EarthParameterSet <: AbstractEarthParameterSet end
const param_set = EarthParameterSet()

function config_simple_box(FT, N, resolution, dimensions; BC = nothing)
    if BC == nothing
        problem = OceanGyre{FT}(dimensions...)
    else
        problem = OceanGyre{FT}(dimensions...; BC = BC)
    end

    _grav::FT = grav(param_set)
    cʰ = sqrt(_grav * problem.H) # m/s
    model = HydrostaticBoussinesqModel{FT}(param_set, problem, cʰ = cʰ)

    config = ClimateMachine.OceanBoxGCMConfiguration(
        "ocean_gyre",
        N,
        resolution,
        param_set,
        model,
    )

    return config
end

function run_ocean_gyre(; imex::Bool = false, BC = nothing, nt = 0, refDat = ())
    FT = Float64

    # DG polynomial order
    N = Int(4)

    # Domain resolution and size
    Nˣ = Int(20)
    Nʸ = Int(20)
    Nᶻ = Int(20)
    resolution = (Nˣ, Nʸ, Nᶻ)

    Lˣ = 4e6    # m
    Lʸ = 4e6    # m
    H = 1000   # m
    dimensions = (Lˣ, Lʸ, H)

    timestart = FT(0)    # s
    timeout = FT(0.25 * 86400) # s
    timeend = FT(86400) # s
    dt = FT(10)    # s

    if imex
        solver_type =
            ClimateMachine.IMEXSolverType(implicit_model = LinearHBModel)
    else
        solver_type = ClimateMachine.ExplicitSolverType(
            solver_method = LSRK144NiegemannDiehlBusch,
        )
    end

    driver_config = config_simple_box(FT, N, resolution, dimensions; BC = BC)

    grid = driver_config.grid
    vert_filter = CutoffFilter(grid, polynomialorder(grid) - 1)
    exp_filter = ExponentialFilter(grid, 1, 8)
    modeldata = (vert_filter = vert_filter, exp_filter = exp_filter)

    solver_config = ClimateMachine.SolverConfiguration(
        timestart,
        timeend,
        driver_config,
        init_on_cpu = true,
        # ode_dt = dt,
        Courant_number = 0.4,
        ode_solver_type = solver_type,
        modeldata = modeldata,
    )

    ClimateMachine.Settings.vtk = "never"
    # vtk_interval = ceil(Int64, timeout / solver_config.dt)
    # ClimateMachine.Settings.vtk = "$(vtk_interval)steps"

    ClimateMachine.Settings.diagnostics = "never"
    # diagnostics_interval = ceil(Int64, timeout / solver_config.dt)
    # ClimateMachine.Settings.diagnostics = "$(diagnostics_interval)steps"

    nt_freq = 620
    cb = ClimateMachine.StateCheck.sccreate(
        [(solver_config.Q, "Q"), (solver_config.dg.state_auxiliary, "s_aux")],
        nt_freq;
        prec = 12,
    )

    result = ClimateMachine.invoke!(solver_config; user_callbacks = [cb])

    ##
    ## Enable the code block below to print table for use in reference value code
    ## reference value code sits in a file named $(@__FILE__)_refvals.jl. It is hand
    ## edited using code generated by block below when reference values are updated.
    ##
    regenRefVals = false
    if regenRefVals
        ## Print state statistics in format for use as reference values
        println(
            "# SC ========== Test number ",
            nt,
            " reference values and precision match template. =======",
        )
        println("# SC ========== $(@__FILE__) test reference values ======================================")
        ClimateMachine.StateCheck.scprintref(cb)
        println("# SC ====================================================================================")
    end
    ##

    ## Check results against reference if present
    checkRefValsPass = true
    if length(refDat) > 0
        checkRefValsPass = ClimateMachine.StateCheck.scdocheck(cb, refDat)
    end

    @test checkRefValsPass
end

@testset "$(@__FILE__)" begin

    nt = 0
    include("refvals/ocean_gyre_refvals.jl")

    # BCs are
    # ( Side BCs,
    #   Ocean floor BCs,
    #   Surface BCc
    # )

    ## By default test with a couple of standard bc's
    #  - noslip on sides and bottom
    #  - freeslip on sides and no-slipt on bottom
    boundary_conditions = [
        (
            OceanBC(Impenetrable(NoSlip()), Insulating()),
            OceanBC(Impenetrable(NoSlip()), Insulating()),
            OceanBC(Penetrable(KinematicStress()), TemperatureFlux()),
        ),
        (
            OceanBC(Impenetrable(FreeSlip()), Insulating()),
            OceanBC(Impenetrable(NoSlip()), Insulating()),
            OceanBC(Penetrable(KinematicStress()), TemperatureFlux()),
        ),
    ]

    for BC in boundary_conditions
        nt = nt + 1
        run_ocean_gyre(
            imex = false,
            BC = BC,
            nt = nt,
            refDat = (refVals[nt], refPrecs[nt]),
        )
    end
end
